dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run during rave
dnl	installation to configure the system for the local environment.

AC_INIT([RAVE],[0.65a])

RAVE_VERSION=0.65a
RAVE_PATCH_LEVEL=0

if test "${prefix}" = "NONE"; then
	prefix=/usr/local/rave
fi

LIBS=-lm

#
# Extract what compiler the python binary was compiled with
#
AC_MSG_CHECKING("for the compiler used for generating python")

PYCC=

PYVERSION=`python -c "import sys; print sys.version[[:3]]"`
PY_EXEC_INSTALLDIR=`python -c "import sys; print sys.exec_prefix"`
PY_CONFIG_MAKE="$PY_EXEC_INSTALLDIR/lib/python$PYVERSION/config/Makefile"
if [[ ! -f "$PY_CONFIG_MAKE" ]]; then
 PY_CONFIG_MAKE="$PY_EXEC_INSTALLDIR/lib64/python$PYVERSION/config/Makefile"
fi
PYCC=`cat $PY_CONFIG_MAKE | sed -n "/^CC=/p" | sed -e "s/CC=//g" | awk '{print $1}'`
AC_MSG_RESULT("$PYCC")
CC=$PYCC

dnl Later on we are going to check for pthread support but since we know that
dnl python might determine pthread flags we can at least see what python
dnl came up with.

AC_MSG_CHECKING(if python CC defines pthread usage)
PYPTHREADCC=`cat $PY_CONFIG_MAKE | sed -n "/^CC=/p" | sed -e "s/CC=//g" | grep "pthread"`
PYGOTPTHREAD=no
if [[ "$PYPTHREADCC" != "" ]]; then
  PYGOTPTHREAD=yes
fi
AC_MSG_RESULT($PYGOTPTHREAD)

AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_CPP
AC_SUBST(AR)
AC_CHECK_PROGS(AR, ar aal, ar)

PY_INCLUDES=
PY_SITEPACK=
PYOPT=
PYLDFLAGS=
PYLDSHARED=
PYCCSHARED=
PYDEFS=

AC_MSG_CHECKING("for python includes")
PY_INCLUDES="$PY_EXEC_INSTALLDIR/include/python$PYVERSION"
AC_MSG_RESULT("$PY_INCLUDES")
PY_INCLUDES="-I$PY_INCLUDES"

AC_MSG_CHECKING("for python lib site-packages")
PY_SITEPACK="$PY_EXEC_INSTALLDIR/lib/python$PYVERSION/site-packages"
AC_MSG_RESULT("$PY_SITEPACK")

dnl Checks for typedefs, structures and compiler characteristics
AC_MSG_CHECKING("for python options")
PYOPT=`cat $PY_CONFIG_MAKE | sed -n "/^OPT=/p" | sed -n -e"s/^OPT=[[ 	]]*\(.*\)/\1/p"`
AC_MSG_RESULT("$PYOPT")

AC_MSG_CHECKING("for ldflags")
PYLDFLAGS=`cat $PY_CONFIG_MAKE | sed -n "/^LDFLAGS=/p" | sed -n -e"s/^LDFLAGS=[[ 	]]*\(.*\)/\1/p"`
AC_MSG_RESULT("$PYLDFLAGS")

AC_MSG_CHECKING("for ldshared")
PYLDSHARED=`cat $PY_CONFIG_MAKE | sed -n "/^LDSHARED=/p" | sed -n -e"s/^LDSHARED=[[ 	]]*\(.*\)/\1/p"`

# Special hack for mac osx.
ISMACOS=no
case `uname -s` in
 Darwin*)
   ISMACOS=yes
   ;;
 darwin*)
   ISMACOS=yes
   ;;
esac

if [[ "x$ISMACOS" = "xyes" ]]; then
  PYLDSHARED=`echo $PYLDSHARED | sed -e "s/[[ \t]]-bundle[[ \t]]/ -dynamiclib /g"`
fi
AC_MSG_RESULT("$PYLDSHARED")

AC_MSG_CHECKING("for ccshared")
PYCCSHARED=`cat $PY_CONFIG_MAKE | sed -n "/^CCSHARED=/p" | sed -n -e"s/^CCSHARED=[[ 	]]*\(.*\)/\1/p"`
AC_MSG_RESULT("$PYCCSHARED")

AC_MSG_CHECKING("for defs")
PYDEFS=`cat $PY_CONFIG_MAKE | sed -n "/^DEFS=/p" | sed -n -e"s/^DEFS=[[ 	]]*\(.*\)/\1/p"`
AC_MSG_RESULT("$PYDEFS")

AC_CHECK_LIB(c,itrunc,GOT_ITRUNC=,GOT_ITRUNC=-DCREATE_ITRUNC)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(math.h stdlib.h)

PROJ_INCLUDE_DIR=
PROJ_LIB_DIR=
GOT_PROJ_INC=no
GOT_PROJ_LIB=no
PROJ_SUPPRESSED=no
AC_ARG_WITH(proj,[  --with-proj=ROOT or INC,LIB       Use the proj library],
		,withval=yes)
case $withval in
	yes)
		AC_CHECK_HEADERS(projects.h,GOT_PROJ_INC=yes)
		AC_CHECK_LIB(proj,pj_inv,GOT_PROJ_LIB=yes)
		;;
	no)
		AC_MSG_CHECKING(for Proj lib)
		AC_MSG_RESULT(suppressed)
		PROJ_SUPPRESSED=yes
		;;
	*)
    if [[ "$withval" != "" ]]; then
      if [[ "`echo $withval | grep ','`" = "" ]]; then
        proj_inc=$withval/include
        proj_lib=$withval/lib
        echo "proj_inc=$proj_inc"
      else
        proj_inc="`echo $withval |cut -f1 -d,`"
        proj_lib="`echo $withval |cut -f2 -d,`"
      fi
      
      if [[ "$proj_inc" != "" ]]; then
        saved_CPPFLAGS="$CPPFLAGS"
        CPPFLAGS="$CPPFLAGS -I$proj_inc"
        AC_CHECK_HEADERS(projects.h,GOT_PROJ_INC=yes)
        if [[ "$GOT_PROJ_INC" = "yes" ]]; then
          PROJ_INCLUDE_DIR="-I$proj_inc"
        else
          CPPFLAGS="$saved_CPPFLAGS"
        fi
      else
        AC_CHECK_HEADERS(projects.h,GOT_PROJ_INC=yes)
      fi

      if [[ "$proj_lib" != "" ]]; then
        saved_LDFLAGS="$LDFLAGS"
        LDFLAGS="$LDFLAGS -L$proj_lib"
        AC_CHECK_LIB(proj,pj_inv,GOT_PROJ_LIB=yes)
        if [[ "$GOT_PROJ_LIB" = "yes" ]]; then
          PROJ_LIB_DIR="-L$proj_lib"
        else
          LDFLAGS="$saved_LDFLAGS"
        fi
      else
        AC_CHECK_LIB(proj,pj_inv,GOT_PROJ_LIB=yes)
      fi
    else
      AC_MSG_ERROR("Odd argument passed for proj")
    fi
    ;;
esac

if [[ "x$PROJ_SUPPRESSED" = "xno" ]]; then
	if [[ "x$GOT_PROJ_INC" = "xno" -o "x$GOT_PROJ_LIB" = "xno" ]]; then
		AC_MSG_ERROR("Could not successfully find the proj library")
	fi
fi

EXPAT_INCLUDE_DIR=
EXPAT_LIB_DIR=
GOT_EXPAT_INC=no
GOT_EXPAT_LIB=no
EXPAT_SUPPRESSED=yes
AC_ARG_WITH(expat,[  --with-expat=ROOT or INC,LIB       Use the expat library],
    ,withval=no)
case $withval in
  yes)
    AC_CHECK_HEADERS(expat.h,GOT_EXPAT_INC=yes)
    AC_CHECK_LIB(expat,XML_ParserCreate,GOT_EXPAT_LIB=yes)
    EXPAT_SUPPRESSED=no
    ;;
  no)
    AC_MSG_CHECKING(for expat lib)
    AC_MSG_RESULT(suppressed)
    EXPAT_SUPPRESSED=yes
    ;;
  *)
    if [[ "$withval" != "" ]]; then
      if [[ "`echo $withval | grep ','`" = "" ]]; then
        expat_inc=$withval/include
        expat_lib=$withval/lib
      else
        expat_inc="`echo $withval |cut -f1 -d,`"
        expat_lib="`echo $withval |cut -f2 -d,`"
      fi
      
      if [[ "$expat_inc" != "" ]]; then
        saved_CPPFLAGS="$CPPFLAGS"
        CPPFLAGS="$CPPFLAGS -I$expat_inc"
        AC_CHECK_HEADERS(expat.h,GOT_EXPAT_INC=yes)
        if [[ "$GOT_EXPAT_INC" = "yes" ]]; then
          EXPAT_INCLUDE_DIR="-I$expat_inc"
        else
          CPPFLAGS="$saved_CPPFLAGS"
        fi
      else
        AC_CHECK_HEADERS(expat.h,GOT_EXPAT_INC=yes)
      fi

      if [[ "$expat_lib" != "" ]]; then
        saved_LDFLAGS="$LDFLAGS"
        LDFLAGS="$LDFLAGS -L$expat_lib"
        AC_CHECK_LIB(expat, XML_ParserCreate, GOT_EXPAT_LIB=yes)
        if [[ "$GOT_EXPAT_LIB" = "yes" ]]; then
          EXPAT_LIB_DIR="-L$expat_lib"
        else
          LDFLAGS="$saved_LDFLAGS"
        fi
      else
        AC_CHECK_LIB(expat, XML_ParserCreate, GOT_EXPAT_LIB=yes)
      fi
    else
      AC_MSG_ERROR("Odd argument passed for expat")
    fi
    EXPAT_SUPPRESSED=no
    ;;
esac

if [[ "$EXPAT_SUPPRESSED" = "no" ]]; then
  if [[ "$GOT_EXPAT_INC" = "no" -o "$GOT_EXPAT_LIB" = "no" ]]; then
    AC_MSG_ERROR("Could not successfully find the expat library")
  fi
fi

dnl -----------------------------
dnl Try to locate the numpy include file 'arrayobject.h' automatically,
dnl if this does not succeed, then the user has to specify this argument
dnl manually.
dnl -----------------------------
AC_ARG_WITH(numpy,[  --with-numpy=INC     Use the numpy include file],
		,withval=yes)
NUMPY_INCLUDE_DIR=
case $withval in
	no)
		AC_MSG_ERROR(You can not remove dependencies to numpy)
		;;
	*)
		numpy_inc="`echo $withval`"
		FOUND_NUMPY_INC=no
		numpy_inc_app=
		
		if test "xyes" = "x$numpy_inc"; then
		  TESTNUMPYDIR=`python -c "import numpy; print eval(\"numpy.__\"+\"file__\")"`
		  if test $? -ne 0; then
			numpy_inc="$PY_EXEC_INSTALLDIR/include/python$PYVERSION"
	      else
			numpy_inc=`echo $TESTNUMPYDIR | sed -e"s/\/__init__.pyc//"`
	      fi
		fi
		if [[ -f $numpy_inc/arrayobject.h ]]; then
			numpy_inc_app=
			FOUND_NUMPY_INC=yes
		elif [[ -f $numpy_inc/numpy/arrayobject.h ]]; then
			numpy_inc_app="/numpy"
			FOUND_NUMPY_INC=yes
		elif [[ -f $numpy_inc/core/include/numpy/arrayobject.h ]]; then
		    numpy_inc_app="/core/include/numpy"
		    FOUND_NUMPY_INC=yes
		elif [[ -f $numpy_inc/numpy/core/include/numpy/arrayobject.h ]]; then
		    numpy_inc_app="/numpy/core/include/numpy"
		    FOUND_NUMPY_INC=yes
		fi
		if test "xyes" != "x$FOUND_NUMPY_INC"; then
			AC_MSG_ERROR(Could not locate arrayobject.h)
		fi
		NUMPY_INCLUDE_DIR="-I$numpy_inc$numpy_inc_app"
		;;
esac

dnl -----------------------------
dnl Is the hlhdf present? It has a header file 'hlhdf.h' and a library -lhlhdf.
dnl It also has a include file pyhlhdf_common.h and a library -lpyhlhdf.
dnl There should also exist a hldef.mk.
dnl If a file hlinstall.sh exists, it is a bonus :)
dnl Their locations might be specified with the --with-hlhdf
dnl command-line switch. The value is an include path and/or a library path.
dnl It the library path is specified then it must be preceded by a comma.
dnl -----------------------------

AC_ARG_WITH(hlhdf,[  --with-hlhdf=ROOT    Use the hlhdf library],
		,withval=yes)

case $withval in
  no)
	AC_MSG_ERROR(You can not remove dependencies to hlhdf)
    ;;
  *)
    hlhdf_root="`echo $withval`"
    if test "xyes" = "x$hlhdf_root"; then
      TESTHLDIR=`python -c "import _pyhl; print eval(\"_pyhl.__\"+\"file__\")"`
      if test $? -ne 0; then
        AC_MSG_ERROR("Could not find hlhdf please specify --with-hlhdf=<hlhdfroot>")
      else
        hlhdf_root=`echo $TESTHLDIR | sed -e"s/\/lib\/_pyhlmodule.so//"`
      fi
    fi
    if [[ -f $hlhdf_root/mkf/hldef.mk ]]; then
      HLHDF_ROOTDIR=$hlhdf_root
    else
      AC_MSG_ERROR("Could not find hlhdf please specify --with-hlhdf=<hlhdfroot>")
    fi
    ;;
esac

dnl Do we want to have BUFR support, default is NO
GOT_BUFR_INC=no
GOT_BUFR_LIB=no
BUFR_INCLUDE_DIR=
BUFR_LIB_DIR=
BUFR_TABLE_DIR=
BUFR_SUPPRESSED=yes
AC_ARG_WITH(bufr,[  --with-bufr=ROOT       Use the bufr library],
		,withval=no)
		
case $withval in
	yes)
		AC_CHECK_HEADERS(bufrlib.h,GOT_BUFR_INC=yes)
		AC_CHECK_LIB(OperaBufr,bufr_read_file,GOT_BUFR_LIB=yes)
		BUFR_SUPPRESSED=no
		;;
	no)
		AC_MSG_CHECKING(for BUFR lib)
		AC_MSG_RESULT(suppressed)
		BUFR_SUPPRESSED=yes
		;;
	*)
    BUFR_SUPPRESSED=no	
    if [[ "$withval" != "" ]]; then
      bufr_inc=$withval/include
      bufr_lib=$withval/lib
      saved_CPPFLAGS="$CPPFLAGS"
      CPPFLAGS="$CPPFLAGS -I$bufr_inc"
      AC_CHECK_HEADERS(bufrlib.h,GOT_BUFR_INC=yes)

      saved_LDFLAGS="$LDFLAGS"
      LDFLAGS="$LDFLAGS -L$bufr_lib"
      AC_CHECK_LIB(OperaBufr,bufr_read_file,GOT_BUFR_LIB=yes)
      
      CPPFLAGS=$saved_CPPFLAGS
      LDFLAGS=$saved_LDFLAGS
      if [[ "$GOT_BUFR_INC" = "yes" -a "$GOT_BUFR_LIB" = "yes" ]]; then
        BUFR_INCLUDE_DIR="-I$withval/include"
        BUFR_LIB_DIR="-L$withval/lib"
      fi
      if [[ -d "$withval/share/bbufr/tables" ]]; then
        BUFR_TABLE_DIR="$withval/share/bbufr/tables"
      fi
    else
      AC_MSG_ERROR("Odd argument passed for bufr")
    fi
    ;;
esac

if [[ "$BUFR_SUPPRESSED" = "no" ]]; then
  if [[ "$GOT_BUFR_INC" = "no" -o "$GOT_BUFR_LIB" = "no" ]]; then
    AC_MSG_ERROR(Could not find opera bufr includes and/or libraries)
  fi
fi

AC_ARG_WITH(bufr-tables,[  --with-bufr-tables=DIR       Use the bufr tables in specified directory as default],
		,withval=no)
case $withval in
	no)
	  if [[ "$BUFR_SUPPRESSED" = "no" ]]; then
	    if [[ "$BUFR_TABLE_DIR" = "" ]]; then
	      AC_MSG_ERROR(You need to specify a directory where the bufr tables can be found)
	    else
	      if [[ ! -d "$BUFR_TABLE_DIR" ]]; then
	        AC_MSG_ERROR(Bad bufr table directory)
	      fi
	    fi
	  fi
	  ;;
    *)
      if [[ "$withval" != "" ]]; then
        if [[ -d "$withval" ]]; then
          BUFR_TABLE_DIR="$withval"
        else
          AC_MSG_ERROR(Bad bufr table directory)
        fi
      else
        if [[ "$BUFR_TABLE_DIR" = "" -o -d "$BUFR_TABLE_DIR" ]]; then
          AC_MSG_ERROR(Bad bufr table directory, have you tried to use with-bufr-tables)
        fi 
      fi
      ;;
esac	  

dnl if [[ "x$BUFR_SUPPRESSED" = "xno" ]]; then
dnl   if [[ "x$BUFR_TABLE_DIR" = "x" ]]; then
dnl     AC_MSG_ERROR("Could not locate any bufr table dir, try  option with bufr tables")
dnl   fi

dnl   if [[ "x$GOT_BUFR_INC" = "xno" -o "x$GOT_BUFR_LIB" = "xno" ]]; then
dnl     AC_MSG_ERROR("Could not successfully find the Opera Bufr library")
dnl   fi
dnl fi

AC_MSG_CHECKING(if pthread support is default)
AC_TRY_RUN([
#include <pthread.h>
void* runme(void*p){return NULL;}
int main(int argc, char** argv) {
  pthread_t p;
  if (pthread_create(&p,NULL,runme,NULL) != 0)
    return 1;
  (void)pthread_detach(p);
  return 0;
}],try_result=yes,try_result=no,try_result=no)
AC_MSG_RESULT($try_result)
PTHREAD_SUPPORT_DEFAULT=$try_result

PTHREAD_CC_OPTION_NEEDED=no

dnl If pthread support isn't default, we need to try out pthread

if [[ "$PTHREAD_SUPPORT_DEFAULT" != "yes" ]]; then
saved_CC="$CC"
CC="$CC -pthread"
AC_MSG_CHECKING(if -pthread option is required)
AC_TRY_RUN([
#include <pthread.h>
void* runme(void*p){return NULL;}
int main(int argc, char** argv) {
  pthread_t p;
  if (pthread_create(&p,NULL,runme,NULL) != 0)
    return 1;
  (void)pthread_detach(p);
  return 0;
}],try_result=yes,try_result=no,try_result=no)
AC_MSG_RESULT($try_result)
CC="$saved_CC"
if [[ "$try_result" != "no" ]]; then
  PTHREAD_CC_OPTION_NEEDED=yes
fi
fi

dnl PYGOTPTHREAD is yes/no depending on if python found pthread option necessary
dnl
GOT_PTHREAD_SUPPORT=no
if [[ "$PTHREAD_CC_OPTION_NEEDED" == "yes" ]]; then
  PYOPT="$PYOPT -pthread"
  LDFLAGS="$LDFLAGS -pthread"
fi

if [[ "$PTHREAD_CC_OPTION_NEEDED" == "yes" -o "$PTHREAD_SUPPORT_DEFAULT" == "yes" ]]; then
  GOT_PTHREAD_SUPPORT=yes
fi

dnl It is possible to turn on memory debugging within RAVE
debugmemory=no
AC_ARG_ENABLE(debug-memory,
  [  --enable-debug-memory     Turns on the rave memory debugging. This should usually not be activated.],
  debugmemory=$enableval)
if [[ "x$debugmemory" = "xyes" ]]; then
  PYOPT="$PYOPT -DRAVE_MEMORY_DEBUG"
fi

HLHDF_INCLUDE_DIR=$HLHDF_ROOTDIR/include
HLHDF_LIB_DIR=$HLHDF_ROOTDIR/lib
HLHDF_INSTALL_BIN=$HLHDF_ROOTDIR/bin/hlinstall.sh
HLHDF_HLDEF_MK_FILE=$HLHDF_ROOTDIR/mkf/hldef.mk

if [[ ! -f $HLHDF_INCLUDE_DIR/hlhdf.h ]]; then
  AC_MSG_ERROR("There is no file $HLHDF_INCLUDE_DIR/hlhdf.h")
fi
if [[ ! -f $HLHDF_LIB_DIR/libhlhdf.a ]]; then
  AC_MSG_ERROR("There is no library $HLHDF_LIB_DIR/libhlhdf.a")
fi
if [[ ! -f $HLHDF_INSTALL_BIN ]]; then
  AC_MSG_ERROR("There is no installation script $HLHDF_INSTALL_BIN")
fi
if [[ ! -f $HLHDF_HLDEF_MK_FILE ]]; then
  AC_MSG_ERROR("There is no mkfile $HLHDF_HLDEF_MK_FILE")
fi

AC_SUBST(PYCC)
AC_SUBST(PY_INCLUDES)
AC_SUBST(PY_SITEPACK)
AC_SUBST(RAVE_VERSION)
AC_SUBST(RAVE_PATCH_LEVEL)
AC_SUBST(PYOPT)
AC_SUBST(PYLDFLAGS)
AC_SUBST(PYLDSHARED)
AC_SUBST(PYCCSHARED)
AC_SUBST(PYDEFS)
AC_SUBST(GOT_ITRUNC)

#Substitute the PROJ arguments
AC_SUBST(PROJ_INCLUDE_DIR)
AC_SUBST(PROJ_LIB_DIR)

#Substitute the numpy argument
AC_SUBST(NUMPY_INCLUDE_DIR)

# Substitute the expat arguments
AC_SUBST(EXPAT_INCLUDE_DIR)
AC_SUBST(EXPAT_LIB_DIR)
AC_SUBST(EXPAT_SUPPRESSED)

# Substitute the bufr arguments
AC_SUBST(BUFR_INCLUDE_DIR)
AC_SUBST(BUFR_LIB_DIR)
AC_SUBST(BUFR_TABLE_DIR)
AC_SUBST(BUFR_SUPPRESSED)

# Substitute the pthread arguments
AC_SUBST(GOT_PTHREAD_SUPPORT)

#Substitutes for the HLHDF library.The HLHDF
#installation contains a mk file that contains
#the paths for HDF5 and possibly ZLIB.
AC_SUBST(HLHDF_ROOTDIR)
AC_SUBST(HLHDF_INCLUDE_DIR)
AC_SUBST(HLHDF_LIB_DIR)
AC_SUBST(HLHDF_INSTALL_BIN)
AC_SUBST(HLHDF_HLDEF_MK_FILE)

AC_CONFIG_FILES(def.mk transform/rave_config.h)
AC_OUTPUT
